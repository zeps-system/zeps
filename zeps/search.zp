(module-extend "zeps"
  (export
    (list "search-info" search-info)
    (list "search" search))

  (loads "vendor/request/request"
         "json/json")

  (search-info #{:help "Search for packages matching a search term."
                  :arguments (("search" "the search term"))})

  (search (lambda (name x)
    (begin
      (import-all "request")
      (let ((try (catch-vm-error `(request:get ,((zeps:get-conf) :zpr-url)))))
        (if (string:starts-with try "getAddrInfo: ")
          (zeps:die "Could not contact zpr server. Got the error message: " try)))
      (let ((res (request:get (++ ((zeps:get-conf) :zpr-url) "packages/search/" name))))
        (case (res :status)
          ((200)
            (let ((packages (json:parse (res :body))))
              (if (null? packages)
                (zeps:status "No package matching the term " name " found.")
                (map (lambda (package)
                       (write (++ (package "name") ": "
                                  (get-from package "description" "No description")
                                  " (Last update: " (package "updated_at") ")")))
                     packages))))
          ((404)
            (zeps:status "No package matching the term " name " found."))
          (else =>
            (lambda (status) (zeps:die "The API returned HTTP " status)))))
      (zeps:success "Search finished.")))))
